WHITESPACE = _{ " " | "\t" | "\r\n" | "\n"}

string_lit = _{ string_lit_dq | string_lit_sq }
string_lit_dq = _{ "\"" ~ string_dq ~ "\"" }
string_lit_sq = _{ "'" ~ string_sq ~ "'" }
string_dq = { (("\\" ~ ("\"" | "\\")) | (!"\"" ~ ANY))* }
string_sq = { (("\\" ~ ("'" | "\\")) | (!"'" ~ ANY))* }

element_access = _{ ("." ~ key) | key_brackets }
index = _{ ASCII_DIGIT+ }
key = _{ (ASCII_ALPHANUMERIC | "_")* }
key_brackets = _{ "[" ~ (string_lit | index) ~ "]" }

sub_path = _{ key ~ element_access* }
path = @{ sub_path }

value = @{ (null | boolean | number | string | array | object) }

boolean = @{ "true" | "false" }
null = @{ "null" }
string = @{ string_lit }
number = @{ "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT+)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }
array = @{ "[" ~ (value ~ ("," ~ value)*)* ~ "]" }
object = @{ "{" ~ (object_inner ~ ("," ~ object_inner)*)* ~ "}" }
object_key = { WHITESPACE* ~ string_dq ~ WHITESPACE* }
object_inner = { object_key ~ ":" ~ value }

statement = { path ~ "=" ~ value ~ ";"? }
statements = _{ SOI ~ statement* ~ EOI }
