WHITESPACE = _{ " " | "\t" | "\r\n" | "\n"}

char = _{
    !("\"" | "\\" | "\'") ~ ANY
    | "\\" ~ ("\"" | "\'" |  "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

string_dq = _{ "\"" ~ key_escaped ~ "\"" }
string_sq = _{ "'" ~ key_escaped ~ "'" }
string_quoted = _{ string_dq | string_sq }

index = _{ ASCII_DIGIT+ }
key = _{ ASCII_ALPHANUMERIC* }
key_escaped = _{ char* }
key_brackets = _{ "[" ~ (string_quoted | index) ~ "]" }

sub_path = _{ key ~ key_brackets* ~ ("." ~ sub_path)* }
path = @{ sub_path }

value = @{ (null | boolean | number | string | array | object) }

boolean = @{ "true" | "false" }
null = @{ "null" }
string = @{ string_quoted }
number = @{ "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT+)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }
array = @{ "[" ~ (value ~ ("," ~ value)*)* ~ "]" }
object = @{ "{" ~ (object_inner ~ ("," ~ object_inner)*)* ~ "}" }
object_key = { WHITESPACE* ~ string_dq ~ WHITESPACE* }
object_inner = { object_key ~ ":" ~ value }

statement = { path ~ "=" ~ value ~ ";"? }
statements = _{ SOI ~ statement* ~ EOI }
